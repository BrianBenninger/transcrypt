#compdef transcrypt

_transcrypt() {
	local curcontext="$curcontext" state line
	typeset -A opt_args

	_arguments \
		'(- 1 *)'{-v,--version}'[print version]' \
		'(- 1 *)'{-h,--help}'[view help message]' \
		'(-c --cipher -d --display -f --flush -u --uninstall)'{-c,--cipher=}'[specify encryption cipher]:cipher:->cipher' \
		'(-p --password -d --display -f --flush -u --uninstall)'{-p,--password=}'[specify encryption password]:password:' \
		'(-y --yes)'{-y,--yes}'[assume yes and accept defaults]' \
		'(-d --display -p --password -c --cipher -r --rekey -u --uninstall)'{-d,--display}'[display current credentials]' \
		'(-r --rekey -d --display -f --flush -u --uninstall)'{-r,--rekey}'[rekey all encrypted files]' \
		'(-f --flush -c --cipher -p --password -r --rekey -u --uninstall)'{-f,--flush}'[flush cached credentials]' \
		'(-u --uninstall -c --cipher -p --password -d --display -r --rekey -f --flush)'{-u,--uninstall}'[uninstall transcrypt]' \
		&& return 0

	case $state in
		cipher)
			ciphers=( ${(f)"$(_call_program available-ciphers openssl list-cipher-commands)"} )
			_describe -t available-ciphers 'available ciphers' ciphers
			;;
	esac
}

_transcrypt "$@"

return 1
